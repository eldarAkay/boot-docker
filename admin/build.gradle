buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven
                {
                    url "https://plugins.gradle.org/m2/"
                }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.4.RELEASE")
        classpath('se.transmode.gradle:gradle-docker:1.2')
        classpath('com.moowork.gradle:gradle-node-plugin:1.2.0')
    }
}


group = 'eldar'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'
apply plugin: 'com.moowork.node'
//apply plugin: 'war'

task webjar(type: Jar) {
    extension 'jar'
    classifier 'webjar'
    from(fileTree("../admin-angular/dist")) {
        into "META-INF/resources"
    }
}

//war {
//    from '../admin-angular/dist' // adds a file-set to the root of the archive
//}


jar {
    baseName = 'gs-spring-boot-docker-admin'
    version = '0.1.0'
}

task buildAngular(type: NpmTask, dependsOn: 'npmInstall') {
    args = ['run', 'build']
}

webjar.dependsOn(buildAngular)
build.dependsOn(webjar)


node {
    version = '7.10.0'
    npmVersion = '4.2.0'
    download = true
    nodeModulesDir = file("../admin-angular")
}

task buildDocker(type: Docker, dependsOn: build) {
    applicationName = jar.baseName
    dockerfile = file('Dockerfile')
    doFirst {
        copy {
            from jar
            into "${stageDir}/target"
        }
    }
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile fileTree(dir: 'build/libs', include: ['admin-webjar.jar'])
    compile group: 'org.webjars', name: 'jquery', version: '2.1.3'
    runtime('org.springframework.boot:spring-boot-devtools')
    testCompile("org.springframework.boot:spring-boot-starter-test")
}